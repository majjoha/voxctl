#compdef voxctl

_voxctl() {
  _arguments -s -C \
    '(- *)'{-h,--help}'[Prints help]: :->noargs' \
    '(- *)'{-v,--version}'[Prints version information]: :->noargs' \
    '1: :__voxctl_commands' \
    '*::arg:->args'

  case "$state" in
    (args)
      case "${line[1]}" in
        (addUrl|playUrl)
          _arguments '1:URL:_urls'
          ;;
      esac
      ;;
  esac
}

(( $+functions[__voxctl_commands] )) ||
__voxctl_commands() {
  local -a commands
  commands=(
    'start:Starts VOX'
    'quit:Quits VOX'
    'play:Begins playback'
    'pause:Pauses playback'
    'togglePlay:Toggles playback between playing and paused '
    'next:Skips to the next track in the playlist'
    'previous:Skips to the previous track in the playlist'
    'shuffle:Toggles shuffle on or off'
    'playUrl:Plays the specified URL'
    'addUrl:Adds the specified URL to the playlist'
    'rewindForward:Rewinds the current track forward'
    'rewindForwardFast:Rewinds the current track forward fast'
    'rewindBackward:Rewinds the current track backward'
    'rewindBackwardFast:Rewinds the current track backward fast'
    'increaseVolume:Increases the volume'
    'decreaseVolume:Decreases the volume'
    'togglePlaylist:Shows/hides the playlist'
    'info:Displays information about the current track in JSON'
    'isRunning:Prints true if VOX is open, otherwise false'
    'help:Prints help'
  )
  _describe -t commands 'commands' commands "$@"
}

_voxctl "$@"
